)body
%---------------------------  ADD/REMOVE VOLS  ---------------------------------
% Option  ===>_ZCMD  +
+ [A]dd volume                                                +
+ [D]elete volume                                             +
+ [L]ist group                                                +
+ [F]ind volume                                               +
+ [0]Back to prev menu                                        +
% GROUP ..... _GRP     +
% VOLUME .... _VOL          +
% SIZE ...... _SIZE+ (9 27 54) default: 9+
+                                                           +
% SAFE DELETE..._Q+
+                                                           +
+ CURRENT SYSTEMS LIST ... &SYSDS                           +
+ CURRENT DISKLIST ....... &DSKLST                          +
+ CURRENT TEMPDS ......... &TEMPDS                          +
+ CURRENT LOGDS .......... &LOGDS                           +
+                                                           +
+ MAXRC .... &MAXRC                                         +
+                                                           +
+                                                           +
)INIT
.CURSOR = ZCMD
&STAT=''
&DSKLST='ED.SMSTOOL.DSKLST'
/*&DSKLST='A09.DSKLST'*/
&SYSDS='ED.SMSTOOL.SYS'
/*&SYSDS='A09.TS'*/
&TEMPDS='A09.TMP'
&LOGDS='ED.SMSTOOL.LOG'
&OPER=''
&MODE=0
&Q='/'
/* MODE FOR CHECK IS DISK ENABLED. 0 - CURRENT DISKS, 1 - ANOTHER DISKS */
)PROC
&MAXRC=''
&ZSEL = &ZCMD
IF (VER (&GRP,NB) & &ZCMD='L')
  &ARGS = 'd sms,storgrp(&GRP),LISTVOL'
  *REXX(ARGS,(DIP#))
IF (VER (&GRP,NB) & &ZCMD='F')
  *REXX(GRP,VOL,(FVOL#))
IF (VER(&GRP,NB) & VER(&VOL,NB) & &VOL NE 'NOT FOUND' & &ZCMD='A')
  *REXX(GRP,VOL,STAT,SIZE,(CHKVOL2#))
  IF (&STAT = 'OK')
    &STAT = ''
    /* CHECK SIZE IF DISK ADDING */
    IF (&SIZE='')
      &SIZE='9'
    IF (&SIZE NE '9' & &SIZE NE '27' & &SIZE NE '54')
      *REXX(SIZE)
      SAY 'SIZE ' SIZE ' NOT ALLOWED. ENTER PLEASE VALID SIZE (9 27 54)'
      *ENDREXX
    ELSE
      *REXX(GRP,VOL,MODE,STAT,(CHKENB#))
      IF (&STAT = 'OK')
        &STAT = ''
        *REXX(DSKLST,SYSDS,TEMPDS,STAT,(CHKALLC#))
        IF (&STAT = 'OK')
          &STAT = ''
          *REXX(DSKLST,ADDR,VOL,SIZE,(FADDR#))
          IF (&ADDR NE 'NOT FOUND')
            *REXX(GRP,VOL,ADDR,SYSDS,SIZE,TEMPDS,LOGDS)
            SAY 'ADDRESS: 'ADDR
            SAY 'VOLUME: 'VOL
            /* RUN ROUTINE WHICH GENERATES JCL WITH SYSTEM COMMAND 'VARY' */
            CALL ALLCJCL# TEMPDS ADDR 'OFFLINE'
            /* READ DS INTO STEM */
            "ALLOC DA('"SYSDS"') F(INPUTDD) SHR REUSE"
            "EXECIO * DISKR INPUTDD (STEM SYSL. FINIS"
            "FREE F(INPUTDD)"
            /* FTPSUB CREATED JCL */
            "CONSOLE ACTIVATE NAME(A09)"
            ADDRESS CONSOLE
            "CART SYSCMD01"
            DO i = 1 to SYSL.0
              SAY 'VARY OFF ' ADDR ' ON 'SYSL.I
              COMMAND="S FTPSUB,SYS="SYSL.i",DS='"TEMPDS"'"
              INTERPRET '"'COMMAND'"'
              CR = GETMSG('SYSRESP.','SOL','SYSCMD01',,5)
            END
            ADDRESS TSO
            "CONSOLE DEACTIVATE"
            /* CREATE INITIALIZATION JCL */
            CALL INIT# TEMPDS ADDR VOL
            /* ONLINE DISK */
            CALL ALLCJCL# TEMPDS ADDR 'ONLINE'
            /* FTPSUB TO ALL SYSTEMS */
            "CONSOLE ACTIVATE NAME(A09)"
            ADDRESS CONSOLE
            "CART SYSCMD01"
            DO i = 1 to SYSL.0
              SAY 'VARY ON ' ADDR ' ON 'SYSL.I
              COMMAND="S FTPSUB,SYS="SYSL.i",DS='"TEMPDS"'"
              INTERPRET '"'COMMAND'"'
              CR = GETMSG('SYSRESP.','SOL','SYSCMD01',,5)
            END
            ADDRESS TSO
            "CONSOLE DEACTIVATE"
            PHRASE = SIZE ' GB DISK('ADDR') ADDED TO GROUP 'GRP' WITH VOL ' VOL
            SAY PHRASE
            *ENDREXX
            &OPER='ADD'
            *REXX(GRP,VOL,ADDR,SIZE,LOGDS,OPER,(SMSLOG#))
            *REXX(MAXRC,OPER,(MAXRC#))
            IF (&MAXRC > 0)
              *REXX()
              SAY 'Max RC > 0. Trying to restore DSKLST'
              *ENDREXX
              *REXX(DSKLST,VOL,STAT,ADDR,Q,OPER,(VDSKLST#))
              IF (&STAT  = 'OK')
                &STAT = ''
                *REXX()
                SAY 'DSKLST SUCCESSFULLY RESTORED'
                *ENDREXX
            &OPER=''
          ELSE
            *REXX(SIZE)
            SAY 'CANT FIND FREE DISK ' SIZE ' GB IN DISKLIST'
            *ENDREXX
        ELSE
          *REXX()
          SAY 'CANT DELETE/CREATE TEMP DATASET OR DISKLIST NOT DEFINED'
          *ENDREXX
      ELSE
        *REXX()
        SAY 'SELECTED DISK NOT ENABLED AT ALL SYSTEMS'
        *ENDREXX
  ELSE
    *REXX(VOL,GRP,STAT)
    SAY VOL ' ALREADY EXIST IN THIS GROUP('GRP') OR NO FREE VOLS'
    *ENDREXX
IF (VER(&GRP,NB) & VER(&VOL,NB) & &VOL NE 'NOT FOUND' & &ZCMD='D')
  *REXX(DSKLST,SYSDS,TEMPDS,STAT,(CHKALLC#))
  IF (&STAT = 'OK')
    &STAT = ''
    *REXX(GRP,VOL,STAT,(CHKVOL#))
    IF (&STAT  = 'OK')
      &STAT = ''
      *REXX(DSKLST,VOL,STAT,ADDR,Q,(VDSKLST#))
      IF (&STAT  = 'OK')
        &STAT = ''
        *REXX(GRP,VOL,ADDR,STAT,SYSDS,TEMPDS)
        SAY VOL ' FOUND AT ' ADDR
        /* VARY OFFLINE */
        /* run routine which generates jcl with system command 'vary' */
        CALL ALLCJCL# TEMPDS ADDR 'OFFLINE'
        /* READ DS INTO STEM */
        "ALLOC DA('"SYSDS"') F(INPUTDD) SHR REUSE"
        "EXECIO * DISKR INPUTDD (STEM SYSL. FINIS"
        "FREE F(INPUTDD)"
        /* FTPSUB CREATED JCL */
        "CONSOLE ACTIVATE NAME(A09)"
        ADDRESS CONSOLE
        "CART SYSCMD01"
        DO i = 1 to SYSL.0
          SAY 'VARY OFF ' ADDR ' AT 'SYSL.I
          COMMAND="S FTPSUB,SYS="SYSL.i",DS='"TEMPDS"'"
          INTERPRET '"'COMMAND'"'
          CR = GETMSG('SYSRESP.','SOL','SYSCMD01',,5)
        END
        ADDRESS TSO
        "CONSOLE DEACTIVATE"
        /* GENERATE REFORMAT JCL */
        /* SUBMIT JCL*/
        CALL REFOR# TEMPDS ADDR VOL
        /* VARY ONLINE */
        /* run routine which generates jcl with system command 'vary' */
        CALL ALLCJCL# TEMPDS ADDR 'ONLINE'
        /* READ DS INTO STEM */
        "ALLOC DA('"SYSDS"') F(INPUTDD) SHR REUSE"
        "EXECIO * DISKR INPUTDD (STEM SYSL. FINIS"
        "FREE F(INPUTDD)"
        /* FTPSUB CREATED JCL */
        "CONSOLE ACTIVATE NAME(A09)"
        ADDRESS CONSOLE
        "CART SYSCMD01"
        DO i = 1 to SYSL.0
          SAY 'VARY ON ' ADDR ' AT 'SYSL.I
          COMMAND="S FTPSUB,SYS="SYSL.i",DS='"TEMPDS"'"
          INTERPRET '"'COMMAND'"'
          CR = GETMSG('SYSRESP.','SOL','SYSCMD01',,5)
        END
        ADDRESS TSO
        "CONSOLE DEACTIVATE"
        SAY 'DISK('ADDR') DELETED FROM 'GRP
        *ENDREXX
        &OPER='DEL'
        *REXX(GRP,VOL,ADDR,SIZE,LOGDS,OPER,(SMSLOG#))
        *REXX(MAXRC,OPER,(MAXRC#))
        IF (&MAXRC > 0)
          *REXX()
          SAY 'Max RC > 0. Trying to restore DSKLST'
          *ENDREXX
          *REXX(DSKLST,VOL,STAT,ADDR,Q,OPER,(VDSKLST#))
          IF (&STAT  = 'OK')
            &STAT = ''
            *REXX()
            SAY 'DSKLST SUCCESSFULLY RESTORED'
            *ENDREXX
        &OPER=''
  ELSE
    *REXX()
    SAY 'CANT DELETE/CREATE TEMP DATASET OR DISKLIST NOT DEFINED'
    *ENDREXX
)HELP
FIELD(ZCMD) PANEL(HSMSADD)
FIELD(VOL)  PANEL(HSMSADD)
FIELD(GRP)  PANEL(HSMSADD)
FIELD(SIZE) PANEL(HSMSADD)
FIELD(Q)    PANEL(HSMSADD)
)end
